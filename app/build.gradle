plugins {
    id "com.android.application"
    id "kotlin-android"
    id "kotlin-kapt"
    id "androidx.navigation.safeargs.kotlin"
}

android {
    compileSdkVersion 30
    ndkVersion "22.1.7171670"

    defaultConfig {
        applicationId "pl.mosenko.sunnypodlaskie"
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "BASEURL", "\"http://api.openweathermap.org/\""
    }
    buildTypes {
        debug {
            minifyEnabled false
            testCoverageEnabled = true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
        externalNativeBuild {
            ndkBuild {
                path "src/main/jni/Android.mk"
            }
        }
    }
    buildFeatures {
        viewBinding true
        dataBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    def koin_version = "3.1.2"
    def room_version = "2.3.0"
    def navigation_version = "2.3.5"
    def lifecycle_version = "2.3.1"
    def retrofit_version = "2.9.0"

    implementation "androidx.multidex:multidex:2.0.1"
    implementation "io.insert-koin:koin-core:$koin_version"
    implementation "io.insert-koin:koin-android:$koin_version"
    implementation "io.insert-koin:koin-android-compat:$koin_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.0"
    implementation "androidx.work:work-runtime-ktx:2.6.0-rc01"
    implementation "androidx.appcompat:appcompat:1.3.1"
    implementation "androidx.activity:activity-ktx:1.3.1"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.fragment:fragment-ktx:1.3.6"
    implementation "androidx.preference:preference-ktx:1.1.1"
    implementation "androidx.media:media:1.4.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.0"
    implementation "androidx.recyclerview:recyclerview:1.2.1"
    implementation "androidx.annotation:annotation:1.2.0"
    implementation "javax.annotation:jsr250-api:1.0"
    implementation "com.google.code.gson:gson:2.8.6"
    implementation "com.google.android.material:material:1.4.0"

    testImplementation "junit:junit:4.13.2"
    testImplementation "org.robolectric:robolectric:4.5.1"
    testImplementation "org.mockito:mockito-core:3.12.4"
    testImplementation "org.mockito.kotlin:mockito-kotlin:3.2.0"
    testImplementation "io.insert-koin:koin-test-junit4:$koin_version"
    testImplementation "com.google.truth:truth:1.1.3"
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.1"
    testImplementation "androidx.arch.core:core-testing:2.1.0"
    androidTestImplementation "androidx.annotation:annotation:1.2.0"
    // Espresso uses version 3.3.0 version on purpose, because of the issue with 3.4.0
    androidTestImplementation "androidx.test.espresso:espresso-core:3.3.0"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.3.0"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test:rules:1.4.0"
    androidTestImplementation "androidx.navigation:navigation-testing:$navigation_version"
    debugImplementation "androidx.fragment:fragment-testing:1.3.6"
}
